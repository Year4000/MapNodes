/*
 * Copyright 2017 Year4000. All Rights Reserved.
 */

plugins {
  id 'java'
  id 'idea'
  id 'de.undercouch.download' version '3.2.0'
  id 'com.moowork.node' version '1.1.1'
}

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'net.year4000.utilities:core:master-SNAPSHOT'
  compile 'com.google.inject:guice:4.0'
  compile 'com.flowpowered:flow-math:1.0.3'
  compile 'com.eclipsesource.j2v8:j2v8_linux_x86_64:4.6.0'
  compile 'org.slf4j:slf4j-api:1.7.21'
}

sourceSets.main.resources {
  srcDir "${projectDir}/src/main/js/"
  srcDir "${projectDir}/src/generated/js/"
}

sourceSets.test.resources {
  srcDir "${projectDir}/src/test/js/"
}

idea.module {
  sourceDirs += file("${projectDir}/src/main/js/")
}

test.testLogging.showStandardStreams = true

// Copy all files to known path
task copyTestJs(type: Copy) {
  from "${projectDir}/src/test/js/"
  from "${projectDir}/src/main/js/"
  from "${projectDir}/src/generated/js/"
  into "${projectDir}/build/test/js/"
}

// Actually Test Node
task testNodeJs(type: NodeTask) {
  //script = file("${projectDir}/build/test/js/test.js")
  script = file("${projectDir}/node_modules/mocha/bin/mocha")
  args = ["${projectDir}/build/test/js/test.js"]
  options = ['--harmony']
}

// Test the javascript files
task testJs {
  dependsOn 'npmInstall'
  dependsOn 'copyTestJs'
  dependsOn 'testNodeJs'
}

test.dependsOn testJs

node {
  version = '7.7.4'
  download = true
}

// Download and shade Javascript utils that MapNodes needs
task generateJsUtils(dependsOn: 'processResources') {
  def depends = [
    'https://raw.githubusercontent.com/lodash/lodash/4.17.4/dist/lodash.js',
    'https://cdnjs.cloudflare.com/ajax/libs/immutable/3.8.1/immutable.js',
    'https://raw.githubusercontent.com/Olical/EventEmitter/v4.2.11/EventEmitter.js',
    'https://raw.githubusercontent.com/jonnyreeves/js-logger/1.3.0/src/logger.js',
    'https://raw.githubusercontent.com/tvcutsem/harmony-reflect/v1.5.1/reflect.js',
    'https://raw.githubusercontent.com/mrdoob/three.js/r84/build/three.js',
    'https://raw.githubusercontent.com/moment/moment/2.18.1/moment.js',
  ]
  // The task to download all needed libs
  download {
    quiet true
    onlyIfNewer true
    src depends
    dest "${projectDir}/src/generated/js/modules/"
  }
}