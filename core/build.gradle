/*
 * Copyright 2019 Year4000. All Rights Reserved.
 */

plugins {
  id 'java'
  id 'idea'
  id 'cpp'
  id 'com.moowork.node' version '1.3.1'
}

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'net.year4000.utilities:core:master-SNAPSHOT'
  compile 'com.google.inject:guice:4.0'
  compile 'com.flowpowered:flow-math:1.0.3'
  compile 'com.eclipsesource.j2v8:j2v8_linux_x86_64:4.8.0'
  compile 'org.slf4j:slf4j-api:1.7.21'
}

node {
  // The version of node gradle will use, reflect this in `webpack.config.js` aswell
  // According to https://nodejs.org/en/download/releases/
  // Node 7.4.0 uses V8 5.4.500.45
  version = '7.4.0'
  download = true
}

sourceSets {
  // Process generated reources such as the JavaScript bundle
  main.resources {
    srcDir "${projectDir}/src/generated/js/"
    srcDir "${projectDir}/src/generated/resources/"
  }
  test.resources {
    srcDir "${projectDir}/src/test/js/"
  }
}

idea.module {
  sourceDirs += file("${projectDir}/src/main/js/")
}

test.testLogging.showStandardStreams = true

// Copy all files to known path
task copyTestJs(type: Copy) {
  from "${projectDir}/src/test/js/"
  from "${projectDir}/src/test/resources/"
  from "${projectDir}/src/main/js/"
  from "${projectDir}/src/main/resources"
  into "${projectDir}/build/test/js/"
}

// Actually Test Node
task testNodeJs(type: NpmTask) {
  args = ['test']
}

// Test the javascript files
task testJs {
  dependsOn 'npmInstall'
  dependsOn 'copyTestJs'
  dependsOn 'testNodeJs'
}

// Run WebPack build to generate resources into the generated resources
task webpackBuild(type: NpmTask) {
  args = ['run', 'build']
}

// Build the javascript bundles
task buildJs {
  dependsOn 'npmInstall'
  dependsOn 'webpackBuild'
}

test.dependsOn testJs
processResources.dependsOn buildJs
