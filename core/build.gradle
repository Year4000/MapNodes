/*
 * Copyright 2017 Year4000. All Rights Reserved.
 */

plugins {
  id 'java'
  id 'idea'
  id 'com.moowork.node' version '1.3.1'
}

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'net.year4000.utilities:core:master-SNAPSHOT'
  compile 'com.google.inject:guice:4.0'
  compile 'com.flowpowered:flow-math:1.0.3'
  compile 'com.eclipsesource.j2v8:j2v8_linux_x86_64:4.8.0'
  compile 'org.slf4j:slf4j-api:1.7.21'
}

sourceSets.main.resources {
  srcDir "${projectDir}/src/generated/js/"
  srcDir "${projectDir}/build/libs/mapnodes/shared/"
}

sourceSets.test.resources {
  srcDir "${projectDir}/src/test/js/"
}

idea.module {
  sourceDirs += file("${projectDir}/src/main/js/")
}

test.testLogging.showStandardStreams = true

// Copy all files to known path
task copyTestJs(type: Copy) {
  from "${projectDir}/src/test/js/"
  from "${projectDir}/src/test/resources/"
  from "${projectDir}/src/main/js/"
  from "${projectDir}/src/main/resources"
  into "${projectDir}/build/test/js/"
}

// Actually Test Node
task testNodeJs(type: NpmTask) {
  args = ['test']
}

// Test the javascript files
task testJs {
  dependsOn 'npmInstall'
  dependsOn 'copyTestJs'
  dependsOn 'testNodeJs'
}

// Run WebPack build to generate resources into the generated resources
task webpackBuild(type: NpmTask) {
  args = ['run', 'build']
}

// Build the javascript bundles
task buildJs {
  dependsOn 'npmInstall'
  dependsOn 'webpackBuild'
}

test.dependsOn testJs
processResources.dependsOn buildJs

node {
  // The version of node gradle will use, reflect this in `webpack.config.js` aswell
  // According to https://nodejs.org/en/download/releases/
  // Node 7.4.0 uses V8 5.4.500.45
  version = '7.4.0'
  download = true
}

// todo install v8 within .gradle and compile it
// todo look at this https://github.com/v8/v8/wiki/Getting-Started-with-Embedding

// This ensures the library is generated before packaging the resources to be added to the jar
processResources.dependsOn 'mapnodesSharedLibrary'

// Compile the C++ JNI files for the project
model {
  components {
    mapnodes(NativeLibrarySpec) {
      sources.cpp {
        source {
          srcDir 'src/main/jni'
          include "**/*.cpp"
        }

        // JNI and extra lib header files
        exportedHeaders {
          srcDir "${System.getProperty('java.home')}/../include"
          srcDir "${System.getProperty('java.home')}/../include/darwin"
          srcDir "${System.getProperty('java.home')}/../include/linux"
          srcDir "${projectDir}/.gradle/v8/include"
          srcDir "${projectDir}/.gradle/v8/include/libplatform"
        }
      }

      buildTypes {
        release
      }
    }
  }

  /*
    Raw Command that gets ran at project root, if any problems just use this command.
    Must have v8 cloned and compiled in the .gradle folder within the core sub module.

    g++ -I ./core/.gradle/v8/include -I /Library/Java/JavaVirtualMachines/jdk1.8.0_131.jdk/Contents/Home/include -I /Library/Java/JavaVirtualMachines/jdk1.8.0_131.jdk/Contents/Home/include/darwin ./core/src/main/jni/net_year4000_mapnodes_v8_V8Engine.cpp -o ./core/build/libs/mapnodes/shared/libmapnodes.dylib -std=c++11 -fPIC -shared -Wl,./core/.gradle/v8/out.gn/x64.release/obj/libv8_lib{base,platform}.a

    clang -I ./core/.gradle/v8/include -I /Library/Java/JavaVirtualMachines/jdk1.8.0_131.jdk/Contents/Home/include -I /Library/Java/JavaVirtualMachines/jdk1.8.0_131.jdk/Contents/Home/include/darwin ./core/src/main/jni/net_year4000_mapnodes_v8_V8Engine.cpp -o ./core/build/libs/mapnodes/shared/libmapnodes.dylib -std=c++11 -fPIC -Wl,-undefined -Wl,dynamic_lookup -Wl,./core/.gradle/v8/out.gn/x64.release/obj/libv8_lib{base,platform}.a
   */
  toolChains {
    all {
      eachPlatform {
        cppCompiler.withArguments { args ->
          args << "-std=c++17"
          args << "-fPIC"
        }
        linker.withArguments { args ->
          args << "-shared"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/libv8_base.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/libv8_libbase.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/libv8_external_snapshot.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/libv8_libplatform.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/libv8_libsampler.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/third_party/icu/libicuuc.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/third_party/icu/libicui18n.a"
          args << "${projectDir}/.gradle/v8/out.gn/x64.release/obj/src/inspector/libinspector.a"
        }
      }
    }

    gcc(Gcc) {}

    clang(Clang) {
      eachPlatform {
        linker.withArguments { args ->
          args << "-undefined"
          args << "dynamic_lookup"
        }
      }
    }
  }
